// Global parameter
params {

    // input file
    // a 3-column (single-end) or 4-column (paired-end) file
    csv_file = "${baseDir}/PE_file.csv" 
    /* Run assembly-based workflow, read-based, or both
      (values need to be one of: "assembly-based", "read-based", or "both")
      It runs both by default
    */
    workflow = "both"
    assay_suffix = "_GLmetagenomics"
    //  additional prefix to add to output files that describe more than one sample (to make them unique compared to other datasets) 
    // leave as empty, i.e. "", if not wanted, include separator at end if adding one, e.g. "Swift1S_"
    additional_filename_prefix = "" 
    publishDir_mode = "link" // "copy", "link", "symlink"	
   
    // Quality trimmed/filtered suffixes
    filtered_R1_suffix = "_R1_filtered.fastq.gz"
    filtered_R2_suffix = "_R2_filtered.fastq.gz"

    // If single-end
    filtered_suffix = "_filtered.fastq.gz"

    // Directories

    // Raw reads directory (can be relative to workflow directory, or needs to be full path)
    raw_reads_dir = "${baseDir}/Raw_Sequence_Data/"
    // output directories (all relative to processing directory, will be created)
    fastqc_out_dir = "${baseDir}/FastQC_Outputs/"
    filtered_reads_dir = "${baseDir}/Filtered_Sequence_Data/"
    assembly_based_dir = "${baseDir}/Assembly-based_Processing/"
    assemblies_dir = "${baseDir}/Assembly-based_Processing/assemblies/"
    genes_dir = "${baseDir}/Assembly-based_Processing/predicted-genes/"
    annotations_and_tax_dir = "${baseDir}/Assembly-based_Processing/annotations-and-taxonomy/"
    mapping_dir = "${baseDir}/Assembly-based_Processing/read-mapping/"
    combined_output_dir = "${baseDir}/Assembly-based_Processing/combined-outputs/"
    bins_dir = "${baseDir}/Assembly-based_Processing/bins/"
    MAGs_dir = "${baseDir}/Assembly-based_Processing/MAGs/"
    read_based_dir = "${baseDir}/Read-based_Processing/"

    // Database creation
    database {
        CAT_DB_LINK = "https://tbb.bio.uu.nl/bastiaan/CAT_prepare/CAT_prepare_20210107.tar.gz"
        cat_db = null // "/path/to/Reference_DBs/CAT_prepare_20210107/"
        ko_db_dir = null  // "/path/to/Reference_DBs/kofamscan_db/"
        metaphlan_db_dir = null // "/path/to/Reference_DBs/metaphlan4-db/"
        chocophlan_dir = null // "/path/to/Reference_DBs/humann3-db/chocophlan/"
        uniref_dir = null // "/path/to/Reference_DBs/humann3-db/uniref/"
        utilities_dir = null  // "/path/to/Reference_DBs/humann3-db/utility_mapping/"
        gtdbtk_db_dir = null // "/path/Reference_DBs/GTDB-tk-ref-db/"
    }

    // Quality assessment parameters
    swift_1S = false
    adapters = "${baseDir}/config/bbtools_adapters.fa"
    multiqc_config = "${baseDir}/config/multiqc.config"


    // Assembly 
    max_mem = 100e9 // 100GB

    // Binning parameters
    reduced_tree = "True"

    // Annotation parameters
    pileup_mem = "5g" // pileup.sh paramater for calculating contig coverage and depth
    block_size = 4  // CAT blocksize

    // ----------  CAT database directory strings -----------------------------------------//
    // The string below will be added to the end of the params.database.cat_db provided above
    // cat taxonomy directory with cat_db path provided above
    cat_taxonomy_dir = "2021-01-07_taxonomy/"
    cat_db_sub_dir = "2021-01-07_CAT_database/"

    // MAG parameters
    min_est_comp = 90
    max_est_redund = 10
    max_est_strain_het = 50

    /*
    Scratch directory for gtdb-tk, if wanting to use disk space instead of RAM, can be memory intensive;
    see https://ecogenomics.github.io/GTDBTk/faq.html#gtdb-tk-reaches-the-memory-limit-pplacer-crashes
    leave empty if wanting to use memory, the default, put in quotes the path to a directory that 
    already exists if wanting to use disk space
    */

    use_gtdbtk_scratch_location = false


    conda{
          // Specify the paths to your existing conda environments
          qc = null // "/path/to/envs/qc"
          humann3 = null //"/path/to/envs/humann3"
          cat = null // "/path/to/envs/CAT"
          prodigal = null // "/path/to/envs/prodigal"
          metabat = null // "/path/to/envs/metabat"
          gtdbtk = null // "/path/to/envs/gtdbtk"
          kegg_decoder = null // "/path/to/envs/kegg_decoder"
          megahit = null // "/path/to/envs/megahit"
          bit = null // "/path/to/envs/bit"
          kofamscan = null // "/path/to/envs/kofamscan"
          mapping = null // "/path/to/envs/mapping"
          checkm = null // "/path/to/envs/checkm"
          }

    GLDS_accession = false
    errorStrategy = "terminate"
}

// Setting the default container engine as singularity
containerEngine = "singularity"
// Conda shouldn't be used be default except when using conda-based profiles
// i.e., slurm_conda and conda
params.use_conda = false


profiles {

    slurm_conda {  
        process.executor = 'slurm' 
        conda.enabled = true
        params.use_conda = true               
    }
    conda {   
        conda.enabled = true
        params.use_conda = true               
    }

    slurm_sing {
        process.executor = 'slurm'
        singularity.enabled    = true
        singularity.autoMounts = true
        singularity.cacheDir = "singularity/" // local singularity images location
        containerEngine = "singularity"
    }    
    singularity {
        singularity.enabled    = true
        singularity.autoMounts = true
        singularity.cacheDir = "singularity/" // local singularity images location
        containerEngine = "singularity"
    }

    docker {
        docker.enabled         = true
        docker.runOptions      = '-u $(id -u):$(id -g)'
        docker.userEmulation   = true
        containerEngine        = "docker"
    }

}

params.DB_ROOT = "${baseDir}/Reference_DBs"
// Number of jobs to run in parallel
executor.queueSize = 10

chocophlanDirExists = {params.database.chocophlan_dir != null}
unirefDirExists = {params.database.uniref_dir != null}
metaphlanDirExists = {params.database.metaphlan_db_dir != null}
utilitiesDirExists = {params.database.utilities_dir != null}


// Mount the databases to their predefined locations in the Biobakery container
if(!chocophlanDirExists ||!unirefDirExists || !metaphlanDirExists || !utilitiesDirExists) {

    //biobakery/humann:3.6 - replace /usr/local/lib/python3.6/dist-packages/humann/data/
    //chocophlan = "${params.DB_ROOT}/humann3-db/chocophlan/:/opt/conda/envs/humann3/lib/python3.10/site-packages/humann/data/chocophlan_DEMO"
    //uniref =  "${params.DB_ROOT}/humann3-db/uniref/:/opt/conda/envs/humann3/lib/python3.10/site-packages/humann/data/uniref_DEMO"
    //utilities = "${params.DB_ROOT}/humann3-db/utility_mapping/:/opt/conda/envs/humann3/lib/python3.10/site-packages/humann/data/misc"
    utilities = "${params.DB_ROOT}/humann3-db/utility_mapping/:/usr/local/lib/python3.6/dist-packages/humann/data/misc"

}else{

    //biobakery/humann:3.6 - replace /usr/local/lib/python3.6/dist-packages/humann/data/
    //chocophlan = "${params.database.chocophlan_dir}:/opt/conda/envs/humann3/lib/python3.10/site-packages/humann/data/chocophlan_DEMO"
    //uniref =  "${params.database.uniref_dir}:/opt/conda/envs/humann3/lib/python3.10/site-packages/humann/data/uniref_DEMO"
    //utilities = "${params.database.utilities_dir}:/opt/conda/envs/humann3/lib/python3.10/site-packages/humann/data/misc"
    utilities = "${params.database.utilities_dir}:/usr/local/lib/python3.6/dist-packages/humann/data/misc"
}



process {
    errorStrategy = { params.errorStrategy  ? params.errorStrategy : "ignore"}
    queue = "normal,priority"
    maxRetries = 2
    memory = '5 GB'
    cache = 'lenient'
    cpus = 8
    //debug = true


    withLabel: bit {
                  cpus = 2
                  conda = {params.conda.bit ? params.conda.bit : "envs/bit.yaml"}
                  container = "olabiyi/bit-astrobiomike:1.0"
                  memory = "5 GB"
            }

// Database set-up
    withLabel: humann_setup {
                  conda = {params.conda.humann3 ? params.conda.humann3 : "envs/humann3.yaml"}
                  container = "biobakery/humann:3.9"
            }

    withName: SETUP_METAPHLAN {
                  memory = "100 GB"
            }


    withLabel: db_setup {
                  storeDir = "${params.DB_ROOT}/"
            }

    withName: SETUP_CAT_DB {
                  conda = {params.conda.cat ? params.conda.cat : "envs/cat.yaml"}
                  container = "olabiyi/bit-astrobiomike:1.0"
            }

    withName: SETUP_KOFAMSCAN_DB {
                  conda = {params.conda.kofamscan ? params.conda.kofamscan : "envs/kofamscan.yaml"}
                  container = "olabiyi/bit-astrobiomike:1.0"
            }

    withName: SETUP_GTDBTK_DB {
                  conda = {params.conda.gtdbtk ? params.conda.gtdbtk : "envs/gtdb-tk.yaml.yaml"}
                  container =  "quay.io/biocontainers/gtdbtk:1.5.0--pyhdfd78af_0"
            }

// Quality control and assesment
    withName: FASTQC {
                  conda = {params.conda.qc ? params.conda.qc : "envs/qc.yaml"}
                  container = "staphb/fastqc:0.12.1"
                  cpus = 2
                  publishDir = [path: params.raw_reads_dir, mode: params.publishDir_mode]
            }

    withName: MULTIQC {
                  conda = {params.conda.qc ? params.conda.qc: "envs/qc.yaml"}
                  container = "staphb/multiqc:1.19"
                  cpus = 2
                  publishDir = [path: params.fastqc_out_dir, mode: params.publishDir_mode]
          }

    withName: BBDUK {
                  conda = {params.conda.qc ? params.conda.qc: "envs/qc.yaml"}
                  container = "staphb/bbtools:38.86"
                  cpus = 5
                  memory = "50 GB"
                  publishDir = [path: params.filtered_reads_dir, mode: params.publishDir_mode]
            }


// Read-based processing

   withLabel: read_based {
              conda = {params.conda.humann3 ? params.conda.humann3 : "envs/humann3.yaml"}
               // this -> "biobakery/humann:3.9" is the latest version
              container = "biobakery/humann:3.9"
              publishDir = [path: params.read_based_dir, mode: params.publishDir_mode]
           }
     

    withName: HUMANN {
               cpus = 8
               memory = "100 GB"
            }

    withName: GEN_READ_BASED_PROCESSING_KO_TABLE {
               containerOptions = { ${containerEngine} == 'singularity' ? "-B ${utilities}" : "-v ${utilities}"}
            }


// Assembly-based proessing

    withLabel: assembly {
               publishDir = [path: params.assemblies_dir, mode: params.publishDir_mode]
              }

    withName: ASSEMBLE {
                  conda = {params.conda.megahit ? params.conda.megahit : "envs/megahit.yaml"}
                  container = "biocontainers/megahit:1.2.9_cv1"
                  cpus = 8
                  memory = "20 GB"
            }

    withLabel: mapping {
                  conda = {params.conda.mapping ? params.conda.mapping : "envs/mapping.yaml"}
                  cpus = 8
                  memory = "5 GB"
            }

    withName: MAPPING {
                  container = "biocontainers/bowtie2:v2.4.1_cv1"
            }

    withName: SAM_TO_BAM {
                  container = "staphb/samtools:1.20"
                  publishDir = [path: params.mapping_dir, mode: params.publishDir_mode]
            }

    withName: CALL_GENES {
                  conda = {params.conda.prodigal ? params.conda.prodigal : "envs/prodigal.yaml"}
                  container = "quay.io/biocontainers/prodigal:2.6.3--h031d066_8"
                  cpus = 8
            }
    
    withLabel: call_genes {
               publishDir = [path: params.genes_dir, mode: params.publishDir_mode]
            }

    withLabel: contig_annotation {
               publishDir = [path: params.annotations_and_tax_dir, mode: params.publishDir_mode]
            }

    withName: KO_ANNOTATION {
                  conda = {params.conda.kofamscan ? params.conda.kofamscan : "envs/kofamscan.yaml"}
                  container = "quay.io/biocontainers/kofamscan:1.3.0--hdfd78af_2"
                  cpus = 8
                  memory = "10 GB"
                  disk = "20 GB"
                  publishDir = [path: params.annotations_and_tax_dir, mode: params.publishDir_mode]
            }

    withName: TAX_CLASSIFICATION {
                  conda = {params.conda.cat ? params.conda.cat : "envs/cat.yaml"}
                  container = "nanozoo/catbat:5.2.3--e9c0a44" 
                  cpus = 8
                  memory = "50 GB"
                  disk = "100 GB"
            }

    withName: GET_COV_AND_DET {
                  conda = {params.conda.mapping ? params.conda.mapping : "envs/mapping.yaml"}
                  container = "staphb/bbtools:38.86"
                  cpus = 8
                  memory = "20 GB"
                  publishDir = [path: params.mapping_dir, mode: params.publishDir_mode]
            }

    withLabel: combine_outputs {
                  publishDir = [path: params.combined_output_dir, mode: params.publishDir_mode]
            }


    withName: METABAT_BINNING {
                  conda = {params.conda.metabat ? params.conda.metabat : "envs/metabat.yaml"}
                  container = "nanozoo/metabat2:2.15--c1941c7"
                  cpus = 8
                  publishDir = [[path: params.mapping_dir, mode: params.publishDir_mode, pattern: "*-metabat-assembly-depth.tsv"],
                                [path: params.bins_dir, mode: params.publishDir_mode, pattern: "*-bin*"]]
            }

    withLabel: bins {
            publishDir = [path: params.bins_dir, mode: params.publishDir_mode]
            } 

    withName: CHECKM_ON_BINS {
                  conda = {params.conda.checkm ? params.conda.checkm : "envs/checkm.yaml"}
                  container = "nanozoo/checkm:1.1.3--c79a047"
                  cpus = 8
                  memory = "50 GB"
                  disk = "50 GB"
            }

    withLabel: mags {
            publishDir = [path: params.MAGs_dir, mode: params.publishDir_mode]
            }

    withName: GTDBTK_ON_MAGS {
                  conda = {params.conda.gtdbtk ? params.conda.gtdbtk : "envs/gtdb-tk.yaml.yaml"}
                  container = "quay.io/biocontainers/gtdbtk:1.5.0--pyhdfd78af_0"
                  containerOptions = { ${containerEngine} == 'singularity' ? "-B \${PWD}:/data -B ${gtdbtk_db_dir}:/refdata" : "-v \${PWD}:/data -v ${gtdbtk_db_dir}:/refdata" } 
                  cpus = 8
                  memory = "600 GB"
                  disk = "700 GB"
            }

    withName:  SUMMARIZE_MAG_KO_ANNOTS_WITH_KEGG_DECODER {
                  conda = {params.conda.kegg_decoder ? params.conda.kegg_decoder : "envs/keggdecoder.yaml"}
                  container = "fmalmeida/keggdecoder:latest"
                  cpus = 8
            }

    withName:  GENERATE_ASSEMBLY_PROCESSING_OVERVIEW_TABLE {
                 publishDir = [path: params.assembly_based_dir, mode: params.publishDir_mode]
            }

}


// Adapted from : https://github.com/nf-core/rnaseq/blob/master/nextflow.config
def trace_timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')
timeline {
    enabled = true
    file    = "${baseDir}/Resource_Usage/execution_timeline_${trace_timestamp}.html"
}
report {
    enabled = true
    file    = "${baseDir}/Resource_Usage/execution_report_${trace_timestamp}.html"
}
trace {
    enabled = true
    file    = "${baseDir}/Resource_Usage/execution_trace_${trace_timestamp}.txt"
}

manifest {
    author = 'Olabiyi Aderemi Obayomi, Mike Douglas Lee'
    homePage = 'https://github.com/nasa/GeneLab_Data_Processing/blob/master/Metagenomics/'
    description = 'GeneLab bioinformatics processing pipelines for metagenomics sequencing data'
    mainScript = 'main.nf'
    defaultBranch = 'main'
    nextflowVersion = '>=22.10.1'
    version = '1.0.0'
}
